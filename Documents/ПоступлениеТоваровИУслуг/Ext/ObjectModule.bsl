#Область ОбработчикиСобытий 
Процедура ОбработкаПроведения(Отказ, Режим)	
	СтруктураУчетнаяПолитика = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата);
	Если СтруктураУчетнаяПолитика.СпособУчетаЗапасов = Перечисления.СпособыСписанияЗапасов.FEFO Тогда
		ОтражатьСрокиГодности = Истина;
	Иначе
		ОтражатьСрокиГодности = Ложь;
	КонецЕсли;
		
	Движения.ТоварыНаСкладах.Записывать = Истина;  
	Движения.Хозрасчетный.Записывать = Истина;
	Движения.УчетЗатрат.Записывать = Истина;      
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =  "ВЫБРАТЬ
	|	ПоступлениеТоваровИУслугТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровИУслугТовары.Цена КАК Цена,
	|	СУММА(ПоступлениеТоваровИУслугТовары.Количество) КАК Количество,
	|	СУММА(ПоступлениеТоваровИУслугТовары.Сумма) КАК Сумма,
	|	ПоступлениеТоваровИУслугТовары.СрокГодности КАК СрокГодности,
	|	ПоступлениеТоваровИУслугТовары.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	|ИЗ
	|	Документ.ПоступлениеТоваровИУслуг.Товары КАК ПоступлениеТоваровИУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровИУслугТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровИУслугТовары.Номенклатура,
	|	ПоступлениеТоваровИУслугТовары.Цена,
	|	ПоступлениеТоваровИУслугТовары.СрокГодности,
	|	ПоступлениеТоваровИУслугТовары.Номенклатура.СчетБухгалтерскогоУчета";     	
	
	ВыборкаИтоги = Запрос.Выполнить().Выбрать(); 
	Пока ВыборкаИтоги.Следующий() Цикл 
		Движение = Движения.ТоварыНаСкладах.Добавить(); 
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период	= Дата;
		Движение.Номенклатура = ВыборкаИтоги.Номенклатура;
		Движение.Сумма = ВыборкаИтоги.Сумма;
		Движение.Склад = Склад;
		Движение.Количество	= ВыборкаИтоги.Количество;       
		
		Если ОтражатьСрокиГодности Тогда 
			Движение.СрокГодности = ВыборкаИтоги.СрокГодности;
		КонецЕсли;	    
		
		Движение = Движения.Хозрасчетный.Добавить();
		Движение.СчетДт = ВыборкаИтоги.СчетБухгалтерскогоУчета;
		Движение.СчетКт = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
		Движение.Период = Дата;
		Движение.Сумма = ВыборкаИтоги.Сумма;
		Движение.Содержание = "Отражено поступление товарно-материальных ценностей от поставщика";
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура] = ВыборкаИтоги.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты] = Контрагент;
	КонецЦикла;
	
	СчетКредита = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;    
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	ПоступлениеУслугУслуги.Ссылка КАК Регистратор,
	|	ПоступлениеТоваровИУслуг.Дата КАК Период,
	|	ПоступлениеУслугУслуги.Услуга КАК Услуга,
	|	ПоступлениеУслугУслуги.Сумма КАК Сумма,
	|	ПоступлениеУслугУслуги.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ПоступлениеУслугУслуги.Цена КАК Цена,
	|	ПоступлениеУслугУслуги.Услуга.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
	|ИЗ
	|	Документ.ПоступлениеТоваровИУслуг.Услуги КАК ПоступлениеУслугУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровИУслуг КАК ПоступлениеТоваровИУслуг
	|		ПО ПоступлениеУслугУслуги.Ссылка = ПоступлениеУслугУслуги.Ссылка
	|ГДЕ
	|	ПоступлениеУслугУслуги.Ссылка = &Ссылка
	|ИТОГИ
	|	СУММА(Сумма),
	|	МАКСИМУМ(Цена)
	|ПО
	|	СтатьяЗатрат,
	|	Услуга";
		
	ВыборкаИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаИтоги.Следующий() Цикл 
		ДвижениеЗатрат = Движения.УчетЗатрат.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеЗатрат,ВыборкаИтоги);
		ДвижениеЗатрат.Период = Дата;   
		
		Выборка = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
		Пока Выборка.Следующий() Цикл
			
			Движение = Движения.Хозрасчетный.Добавить();
			Движение.СчетДт = Выборка.СчетБухгалтерскогоУчета;
			Движение.СчетКт = СчетКредита;
			Движение.Период = Дата;
			Движение.Сумма = Выборка.Сумма;
			Движение.Содержание = "Отражено поступление услуг от поставщика";
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетДт, Движение.СубконтоДт, Выборка.СтатьяЗатрат);
			БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетКт, Движение.СубконтоКт, Контрагент); 
		КонецЦикла;	
	КонецЦикла; 
КонецПроцедуры
#КонецОбласти