#Область ОбработчикиСобытий
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДатаПрайсЛиста = ТекущаяДата();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы 
&НаКлиенте
Процедура ЗагрузитьПрайсЛист(Команда)
	СкопироватьФайлНаСервер("ТабличныйДокумент");
КонецПроцедуры
#КонецОбласти            

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура СкопироватьФайлНаСервер(СпособЗагрузки)
	ОповещениеОЗавершении = Новый ОписаниеОповещения("СкопироватьФайлНаСерверЗавершение", ЭтотОбъект, СпособЗагрузки);
	НачатьПомещениеФайлаНаСервер(ОповещениеОЗавершении,,,,,УникальныйИдентификатор);
КонецПроцедуры 

&НаКлиенте
Процедура СкопироватьФайлНаСерверЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	Если ОписаниеПомещенногоФайла <> Неопределено Тогда
		АдресФайлаВХранилище = ОписаниеПомещенногоФайла.Адрес; 
		ЗагрузитьИзТабличногоДокументаНаСервере(АдресФайлаВХранилище, ДатаПрайсЛиста, ВидЦеныПродажи, ДополнительныеПараметры); 
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗагрузитьИзТабличногоДокументаНаСервере(АдресФайлаВХранилище, ДатаПрайсЛиста, ВидЦеныПродажи, ДополнительныеПараметры)
	ТабДок = ПрочитатьФайл(АдресФайлаВХранилище); 
	Если ДополнительныеПараметры = "ТабличныйДокумент" Тогда
		ТаблицаПрайсЛиста = ЗаполнитьТаблицуЗначенийИзТабличногоДокумента(ТабДок);
	Конецесли;    
	
	Если ТаблицаПрайсЛиста.Количество() Тогда    
		ЗагрузитьПрайсЛистПоставщика(ТаблицаПрайсЛиста, ДатаПрайсЛиста, ВидЦеныПродажи);
	Иначе
		СообщениеПользователю = Новый СообщениеПользователю();
		СообщениеПользователю.Текст = "Выбранный файл не содержит строк с ценами!";
		СообщениеПользователю.Сообщить();
	КонецЕсли;   
КонецПроцедуры

&НаСервереБезКонтекста                            
Функция ПрочитатьФайл(АдресФайлаВХранилище)	
	ТабДок = Новый ТабличныйДокумент;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".xlsx"); 
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайлаВХранилище); 
	ДвоичныеДанные.Записать(ИмяВременногоФайла); 
	
	Попытка
		ТабДок.Прочитать(ИмяВременногоФайла); 
	Исключение  
		вызватьИсключение "Не удалось прочитать файл EXCEL в табличный документ!";
	КонецПопытки; 
	
	возврат ТабДок;	
КонецФункции 

&НаСервереБезКонтекста
Функция ЗаполнитьТаблицуЗначенийИзТабличногоДокумента(ТабДок)
	ТаблицаПрайсЛиста = Новый ТаблицаЗначений;
	ТаблицаПрайсЛиста.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(100)));
	ТаблицаПрайсЛиста.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(9)));
	ТаблицаПрайсЛиста.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число"));
	
	КоличествоСтрок = табДок.ВысотаТаблицы;
	
	Для сч = 2 По КоличествоСтрок Цикл		
		СтрокаПрайса = ТаблицаПрайсЛиста.Добавить();
		Попытка
			СтрокаПрайса.Номенклатура = Строка(ТабДок.ПолучитьОбласть("R" + Формат(сч, "ЧГ=0") + "C2").ТекущаяОбласть.Текст); 
			СтрокаПрайса.Код = Строка(ТабДок.ПолучитьОбласть("R" + Формат(сч, "ЧГ=0;") + "C3").ТекущаяОбласть.Текст);
			СтрокаПрайса.Цена = Число(ТабДок.ПолучитьОбласть("R" + Формат(сч, "ЧГ=0;") + "C4").ТекущаяОбласть.Текст); 
		Исключение
			вызватьИсключение "Не удалось прочитать файл EXCEL в табличный документ!";
		КонецПопытки;		
	КонецЦикла;
	
	возврат ТаблицаПрайсЛиста;
КонецФункции  

&НаСервереБезКонтекста
Процедура ЗагрузитьПрайсЛистПоставщика(ТаблицаПрайсЛиста, ДатаПрайсЛиста, ВидЦеныПродажи)	
	Запрос = Новый Запрос; 
	Запрос.УстановитьПараметр("ТаблицаПрайсЛиста", ТаблицаПрайсЛиста);	
	Запрос.Текст  = "ВЫБРАТЬ
	|	ТаблицаПрайсаТЗ.Номенклатура КАК Номенклатура,
	|	ТаблицаПрайсаТЗ.Код КАК Код,
	|	ТаблицаПрайсаТЗ.Цена КАК Цена
	|ПОМЕСТИТЬ ВТ_ТаблицаПрайсЛиста
	|ИЗ
	|	&ТаблицаПрайсЛиста КАК ТаблицаПрайсаТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаПрайсЛиста.Код КАК Код,
	|	ВТ_ТаблицаПрайсЛиста.Цена КАК Цена,
	|	Спр_Номенклатура.Ссылка КАК Номенклатура
	|ИЗ
	|	Справочник.Номенклатура КАК Спр_Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаПрайсЛиста КАК ВТ_ТаблицаПрайсЛиста
	|		ПО Спр_Номенклатура.Код = ВТ_ТаблицаПрайсЛиста.Код";
	
	Рез = Запрос.Выполнить();
	Если Рез.Пустой() Тогда
		возврат;
	КонецЕсли;
	
	Выборка = Рез.Выбрать();
	КоличествоПозиций = 0;	
	Пока Выборка.Следующий() Цикл
		НоваяЗапись = РегистрыСведений.ЦеныНоменклатуры.СоздатьМенеджерЗаписи();  
		НоваяЗапись.Номенклатура = Выборка.Номенклатура;
		НоваяЗапись.Цена = Выборка.Цена; 
		НоваяЗапись.Период = ДатаПрайсЛиста;
		НоваяЗапись.ВидЦены = ВидЦеныПродажи; 
		Попытка	
			НоваяЗапись.Записать(); 
		Исключение
			СообщениеПользователю = Новый СообщениеПользователю();
			СообщениеПользователю.Текст = "Произошла ошибка при записи регистра сведений!";
			СообщениеПользователю.Сообщить();	
		КонецПопытки;  
		КоличествоПозиций = КоличествоПозиций + 1;	
	КонецЦикла; 
	СообщениеПользователю = Новый СообщениеПользователю();
	СообщениеПользователю.Текст = СтрШаблон("Цены номенклатуры в количестве ""%1"" успешно записаны",КоличествоПозиций);
	СообщениеПользователю.Сообщить();
Конецпроцедуры
#КонецОбласти